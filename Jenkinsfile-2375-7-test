import org.jenkinsci.plugins.workflow.libs.Library
@Library('jenkins-pipeline-shared-libraries')_

deployProperties = [:]

pipeline {
    agent {
        label 'kie-rhel7-priority'
    }
    environment {
        PROPERTIES_FILE_NAME = "deployment.properties"
        BOT_CREDENTIALS_ID = "bsig-gh-bot"
    }
    parameters {
        string(name: 'DISPLAY_NAME', defaultValue: '', description: 'Setup a specific build display name')
        string(name: 'DEPLOY_BUILD_URL', defaultValue: 'https://rhba-jenkins.rhev-ci-vms.eng.rdu2.redhat.com/job/custom/job/kmok/job/KOGITO-2375-props/lastSuccessfulBuild/', description: 'URL to jenkins deploy build to retrieve the `deployment.properties` file. If base parameters are defined, they will override the `deployment.properties` information')
        string(name: 'GIT_AUTHOR', defaultValue: 'Kevin-Mok', description: 'Which Git author repository ?')
        string(name: 'BUILD_BRANCH_NAME', defaultValue: '7.13.0', description: 'Override `deployment.properties`. Which branch to build? Set if you are not on a multibranch pipeline.')
        booleanParam(name: 'RELEASE', defaultValue: true, description: 'Override `deployment.properties`. Is this build for a release?')
        string(name: 'PROJECT_VERSION', defaultValue: '7.13.0', description: 'Override `deployment.properties`. Optional if not RELEASE. If RELEASE, cannot be empty.')
        string(name: 'GIT_TAG', defaultValue: '7.13.01', description: 'Git tag to set, if different from PROJECT_VERSION')
    }
    stages {
        stage('Initialization') {
            steps {
                script {
                    cleanWs()
                    if (params.DISPLAY_NAME != "") {
                        currentBuild.displayName = params.DISPLAY_NAME
                    }

                    readDeployProperties()

                    if (isRelease()) {
                        assert getProjectVersion() != ''
                    }
                }
            }
        }
        stage('Merge runtimes deploy PR and tag') {
            when {
                expression {
                    return isRelease() &&
                           getDeployProperty('jenkins-test.pr.link') != '' &&
                           getGitTag() != ''
                }
            }
            steps{
                dir("kogito-runtimes") {
                    checkoutRepo("jenkins-test")
                    /* mergeAndPush("jenkins-test") */
                    tagLatest()
                }
            }
        }
        stage('Create/merge runtimes snapshot PR'){
            when {
                expression { return isRelease() && getProjectVersion() != "" }
            }
            steps {
                dir("kogito-runtimes-bot") {
                    prepareForPR("jenkins-test")
                    sh "echo \$(date) >> README.md"
                    commitAndCreatePR("jenkins-test")
                }
                dir("kogito-runtimes") {
                    script {
                        if (getDeployProperty('jenkins-test.pr.link') != ''){
                            mergeAndPush("jenkins-test")
                        }
                    }
                }
            }
        }
    }
}

//////////////////////////////////////////////////////////////////////////////
// Deployment properties
//////////////////////////////////////////////////////////////////////////////

void readDeployProperties(){
    if (params.DEPLOY_BUILD_URL != ''){
        sh "wget ${params.DEPLOY_BUILD_URL}artifact/${PROPERTIES_FILE_NAME} -O ${PROPERTIES_FILE_NAME}"
        deployProperties = readProperties file: PROPERTIES_FILE_NAME
        // echo all properties
        echo deployProperties.collect{ entry -> "${entry.key}=${entry.value}" }.join("\n")
    }
}

boolean hasDeployProperty(String key){
    return deployProperties[key] != null
}

String getDeployProperty(String key){
    if(hasDeployProperty(key)){
        return deployProperties[key]
    }
    return ""
}

String getParamOrDeployProperty(String paramKey, String deployPropertyKey){
    if (params[paramKey] != ""){
        return params[paramKey]
    }
    return getDeployProperty(deployPropertyKey)
}

//////////////////////////////////////////////////////////////////////////////
// Getter / Setter
//////////////////////////////////////////////////////////////////////////////

boolean isRelease() {
    return params.RELEASE || (getDeployProperty("release") == "true")
}

String getProjectVersion() {
    return getParamOrDeployProperty("PROJECT_VERSION", "project.version")
}

String getSnapshotVersion() {
    return util.getNextSnapshot(getProjectVersion())
}

String getGitTag() {
    return params.GIT_TAG != "" ? params.GIT_TAG : getProjectVersion()
}

String getBuildBranch() {
    return getParamOrDeployProperty("BUILD_BRANCH_NAME", "git.branch")
}

String getGitAuthor() {
    return getParamOrDeployProperty("GIT_AUTHOR", "git.author")
}

//////////////////////////////////////////////////////////////////////////////
// Git
//////////////////////////////////////////////////////////////////////////////

void checkoutRepo(String repo) {
    checkout(githubscm.resolveRepository(repo, getGitAuthor(), getBuildBranch(), false))
    sh "git checkout ${getBuildBranch()}"
}

void mergeAndPush(String repo) {
    githubscm.mergePR(getDeployProperty("${repo}.pr.link"), "kmok-github")
    githubscm.pushObject("origin", getBuildBranch(), "kmok-github")
}

void tagLatest() {
    githubscm.tagRepository(getGitTag(), env.BUILD_TAG)
    githubscm.pushObject("origin", "--tags ${getGitTag()}", "kmok-github")
}

void prepareForPR(String repo) {
    checkout(githubscm.resolveRepository(repo, getGitAuthor(), getBuildBranch(), false))
    githubscm.forkRepo(env.BOT_CREDENTIALS_ID)
    githubscm.createBranch(getSnapshotVersion())
}

void addNotIgnoredPoms() {
    // based on https://stackoverflow.com/a/59888964/8811872
    sh '''
    find . -type f -name 'pom.xml' > found_poms.txt
    poms_to_add=""
    while IFS= read -r pom; do
        if ! git check-ignore -q "\$pom"; then
            poms_to_add="\$poms_to_add \$pom"
        fi
    done < found_poms.txt
    rm found_poms.txt
    git add \$poms_to_add
    '''
}

void commitAndCreatePR(String repo) {
    def commitMsg = "Update snapshot version to ${getSnapshotVersion()}"
    def prBody = "Generated by build ${BUILD_TAG}: ${BUILD_URL}"
    // Not using githubscm.commitChanges() because globbing won't work.
    // See: https://github.com/kiegroup/kogito-runtimes/pull/570#discussion_r449268738
    addNotIgnoredPoms()
    sh "git commit -m '${commitMsg}'"
    githubscm.pushObject("origin", getSnapshotVersion(), env.BOT_CREDENTIALS_ID)
    deployProperties["${repo}.pr.link"] = githubscm.createPR(commitMsg, prBody, getBuildBranch(), env.BOT_CREDENTIALS_ID)
}

